set(CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS "")
set(CONSTITUTIVE_LAWS_APPLICATION_COMPILE_DEFS "")

## generate variables with the sources
set( CONSTITUTIVE_LAWS_APPLICATION_SOURCES
    ${CMAKE_CURRENT_SOURCE_DIR}/constitutive_laws_application.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/constitutive_laws_application_variables.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_python/constitutive_laws_python_application.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_python/add_constitutive_laws_to_python.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/constitutive_laws/udsm.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/constitutive_laws/udsme.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/constitutive_laws/umat2.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/constitutive_laws/umat3.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/constitutive_laws/umat3e.cpp
)

if(CMAKE_Fortran_COMPILER)
    set(CONSTITUTIVE_LAWS_APPLICATION_COMPILE_DEFS "${CONSTITUTIVE_LAWS_APPLICATION_COMPILE_DEFS};KRATOS_ENABLE_FORTRAN_CODE")
    set(CONSTITUTIVE_LAWS_APPLICATION_SOURCES
        ${CONSTITUTIVE_LAWS_APPLICATION_SOURCES}
        ${CMAKE_CURRENT_SOURCE_DIR}/constitutive_laws/umat.cpp
        ${CMAKE_CURRENT_SOURCE_DIR}/custom_external_libraries/umat/mises_umat.f
        ${CMAKE_CURRENT_SOURCE_DIR}/custom_external_libraries/umat/umat_hypo.f
        ${CMAKE_CURRENT_SOURCE_DIR}/custom_external_libraries/umat/umat_fortran_wrapper.f
        ${CMAKE_CURRENT_SOURCE_DIR}/custom_external_libraries/umat/xit.f
    )
endif()

if(DEFINED UMAT_LIBRARY)
    set(CONSTITUTIVE_LAWS_APPLICATION_COMPILE_DEFS "${CONSTITUTIVE_LAWS_APPLICATION_COMPILE_DEFS};KRATOS_UMAT_LIBRARY_IS_PROVIDED")
endif()
if(DEFINED UDSM_LIBRARY)
    set(CONSTITUTIVE_LAWS_APPLICATION_COMPILE_DEFS "${CONSTITUTIVE_LAWS_APPLICATION_COMPILE_DEFS};KRATOS_UDSM_LIBRARY_IS_PROVIDED")
endif()

if(DEFINED OPENSEES_DIR)
    set(CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS "${CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS};${OPENSEES_DIR}")
    set(CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS "${CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS};${OPENSEES_DIR}/SRC")
    set(CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS "${CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS};${OPENSEES_DIR}/SRC/material")
    set(CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS "${CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS};${OPENSEES_DIR}/SRC/material/nD")
    set(CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS "${CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS};${OPENSEES_DIR}/SRC/material/nD/UWmaterials")
    set(CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS "${CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS};${OPENSEES_DIR}/SRC/matrix")
    set(CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS "${CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS};${OPENSEES_DIR}/SRC/domain/component")
    set(CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS "${CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS};${OPENSEES_DIR}/SRC/tagged")
    set(CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS "${CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS};${OPENSEES_DIR}/SRC/handler")
    set(CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS "${CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS};${OPENSEES_DIR}/SRC/actor/actor")
    set(CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS "${CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS};${OPENSEES_DIR}/SRC/recorder/response")
    set(CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS "${CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS};${OPENSEES_DIR}/SRC/element")
    find_library( OPENSEES_LIBRARY NAMES OpenSees PATHS ${OPENSEES_DIR}/lib NO_DEFAULT_PATH )
    find_library( OPENSEES_BLAS_LIBRARY NAMES Blas PATHS ${OPENSEES_DIR}/lib NO_DEFAULT_PATH )
    find_library( OPENSEES_LAPACK_LIBRARY NAMES Lapack PATHS ${OPENSEES_DIR}/lib NO_DEFAULT_PATH )
    set(OPENSEES_LIBRARIES ${OPENSEES_LIBRARY} ${OPENSEES_LAPACK_LIBRARY} ${OPENSEES_BLAS_LIBRARY})
    set(CONSTITUTIVE_LAWS_APPLICATION_COMPILE_DEFS "${CONSTITUTIVE_LAWS_APPLICATION_COMPILE_DEFS};KRATOS_USE_OPENSEES")
    message("OpenSees directory:" ${OPENSEES_DIR})
    message("OpenSees libraries:" ${OPENSEES_LIBRARIES})
    set(CONSTITUTIVE_LAWS_APPLICATION_SOURCES
        ${CONSTITUTIVE_LAWS_APPLICATION_SOURCES}
        ${CMAKE_CURRENT_SOURCE_DIR}/constitutive_laws/opensees_mat.cpp
    )
endif()

###############################################################
add_library(KratosConstitutiveLawsApplicationDependencies INTERFACE)
if((${CMAKE_SYSTEM_NAME} MATCHES "Linux"))
    target_link_libraries( KratosConstitutiveLawsApplicationDependencies INTERFACE gfortran )
    target_link_libraries( KratosConstitutiveLawsApplicationDependencies INTERFACE dl )
endif()
if(DEFINED OPENSEES_DIR)
    target_link_libraries( KratosConstitutiveLawsApplicationDependencies INTERFACE ${OPENSEES_LIBRARIES} )
endif()
if(DEFINED UMAT_LIBRARY)
    target_link_libraries( KratosConstitutiveLawsApplicationDependencies INTERFACE ${UMAT_LIBRARY} )
endif()
if(DEFINED UDSM_LIBRARY)
    target_link_libraries( KratosConstitutiveLawsApplicationDependencies INTERFACE ${UDSM_LIBRARY} )
endif()

## define library Kratos which defines the basic python interface
add_library(KratosConstitutiveLawsApplication SHARED ${CONSTITUTIVE_LAWS_APPLICATION_SOURCES} )
target_link_libraries( KratosConstitutiveLawsApplication PRIVATE KratosCore )
target_link_libraries( KratosConstitutiveLawsApplication PRIVATE KratosStructuralApplication )
target_link_libraries( KratosConstitutiveLawsApplication PUBLIC KratosConstitutiveLawsApplicationDependencies )

target_include_directories( KratosConstitutiveLawsApplication PUBLIC ${CONSTITUTIVE_LAWS_APPLICATION_INCLUDE_DIRS})

target_compile_definitions(KratosConstitutiveLawsApplication PRIVATE KRATOS_CORE=IMPORT CONSTITUTIVE_LAWS_APPLICATION=EXPORT STRUCTURAL_APPLICATION=IMPORT)
target_compile_definitions(KratosConstitutiveLawsApplication PUBLIC ${CONSTITUTIVE_LAWS_APPLICATION_COMPILE_DEFS})

set_target_properties(KratosConstitutiveLawsApplication PROPERTIES PREFIX "")
install(TARGETS KratosConstitutiveLawsApplication DESTINATION libs )
###############################################################

## changing the .dll suffix to .pyd for Windows compatibility
if(${CMAKE_SYSTEM_NAME} MATCHES "Windows")
    set_target_properties(KratosConstitutiveLawsApplication PROPERTIES SUFFIX .pyd)
endif(${CMAKE_SYSTEM_NAME} MATCHES "Windows")
#######################################################################

if(${INSTALL_PYTHON_FILES} MATCHES ON)
  get_filename_component (CURRENT_DIR_NAME ${CMAKE_CURRENT_SOURCE_DIR} NAME)
  install(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/python_scripts DESTINATION applications/${CURRENT_DIR_NAME}  FILES_MATCHING PATTERN "*.py"  PATTERN ".svn" EXCLUDE)
endif(${INSTALL_PYTHON_FILES} MATCHES ON)
#######################################################################

# Add to the KratosMultiphisics Python module
install(FILES "${CMAKE_CURRENT_SOURCE_DIR}/ConstitutiveLawsApplication.py" DESTINATION KratosMultiphysics )
#######################################################################
